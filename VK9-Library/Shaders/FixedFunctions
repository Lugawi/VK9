/*
Copyright(c) 2017 Christopher Joseph Dean Schaefer

This software is provided 'as-is', without any express or implied
warranty.In no event will the authors be held liable for any damages
arising from the use of this software.

Permission is granted to anyone to use this software for any purpose,
including commercial applications, and to alter it and redistribute it
freely, subject to the following restrictions :

1. The origin of this software must not be misrepresented; you must not
claim that you wrote the original software.If you use this software
in a product, an acknowledgment in the product documentation would be
appreciated but is not required.
2. Altered source versions must be plainly marked as such, and must not be
misrepresented as being the original software.
3. This notice may not be removed or altered from any source distribution.
*/

//https://msdn.microsoft.com/en-us/library/windows/desktop/bb172616(v=vs.85).aspx
vec4 calculateResult(uint operation, vec4 argument1, vec4 argument2, vec4 argument0, float alpha, float factorAlpha)
{
	vec4 result = vec4(1.0,1.0,1.0,1.0);

	switch(operation)
	{
		case D3DTOP_DISABLE:
		break;
		case D3DTOP_SELECTARG1:
			result = argument1;
		break;
		case D3DTOP_SELECTARG2:
			result = argument2;
		break;
		case D3DTOP_MODULATE:
			result = argument1 * argument2;
		break;
		case D3DTOP_MODULATE2X:
			result = (argument1 * argument2) * 2;
		break;
		case D3DTOP_MODULATE4X:
			result = (argument1 * argument2) * 4;
		break;
		case D3DTOP_ADD:
			result = argument1 + argument2;
		break;
		case D3DTOP_ADDSIGNED:
			result = argument1 + argument2 - 0.5;
		break;
		case D3DTOP_ADDSIGNED2X:
			result = (argument1 + argument2 - 0.5) * 2;
		break;
		case D3DTOP_SUBTRACT:
			result = argument1 - argument2;
		break;
		case D3DTOP_ADDSMOOTH:
			result = argument1 + argument2 - argument1 * argument2;
		break;
		case D3DTOP_BLENDDIFFUSEALPHA:
			result = argument1 * (alpha) + argument2 * (1 - alpha);
		break;
		case D3DTOP_BLENDTEXTUREALPHA:
			result = argument1 * (alpha) + argument2 * (1 - alpha);
		break;  
		case D3DTOP_BLENDFACTORALPHA:
			result = argument1 * (factorAlpha) + argument2 * (1 - factorAlpha);
		break;
		case D3DTOP_BLENDTEXTUREALPHAPM:
			result = argument1 + argument2 * (1 - alpha);
		break;
		case D3DTOP_BLENDCURRENTALPHA:
			result = argument1 * (alpha) + argument2 * (1 - alpha);
		break;
		case D3DTOP_PREMODULATE:
			result = argument1; //TODO figure out n+1 logic.
		break;
		case D3DTOP_MODULATEALPHA_ADDCOLOR:
			result.rgb = argument1.rgb + argument1.a * argument2.rgb;
		break;
		case D3DTOP_MODULATECOLOR_ADDALPHA:
			result.rgb = argument1.rgb * argument2.rgb + argument1.a;
		break;
		case D3DTOP_MODULATEINVALPHA_ADDCOLOR:
			result.rgb = (1 - argument1.a) * argument2.rgb + argument1.rgb;
		break;
		case D3DTOP_MODULATEINVCOLOR_ADDALPHA:
			result.rgb = (1 - argument1.rgb) * argument2.rgb + argument1.a;
		break;
		case D3DTOP_BUMPENVMAP:
			//TODO: figure out per-pixel bump mapping.
		break;
		case D3DTOP_BUMPENVMAPLUMINANCE:
			//TODO: figure out per-pixel bump mapping.
		break;
		case D3DTOP_DOTPRODUCT3:
			//result.a = (argument1.r * argument2.r) + (argument1.g * argument2.g) + (argument1.b * argument2.b);

			result.a = 4*((argument1.r - 0.5)*(argument2.r - 0.5) + (argument1.g - 0.5)*(argument2.g - 0.5) + (argument1.b - 0.5)*(argument2.b - 0.5));
			result.r = result.a;
			result.g = result.a;
			result.b = result.a;
		break;
		case D3DTOP_MULTIPLYADD:
			result = argument0 + argument1 * argument2;
		break;
		case D3DTOP_LERP:
			result = (argument0) * argument1 + (1 - argument0) * argument2;
		break;
		default:
			//Nothing
		break;
	}

	return result;
}

vec4 getStageArgument(uint argument,vec4 temp,uint constant,vec4 result,sampler2D tex,vec2 texcoord)
{
	vec4 realResult;
	switch(argument & D3DTA_SELECTMASK)
	{
		case D3DTA_CONSTANT:
			realResult = Convert(constant);
		break;
		case D3DTA_CURRENT:
			realResult = result;
		break;
		case D3DTA_DIFFUSE:
			realResult = diffuseColor;
		break;
		case D3DTA_SPECULAR:
			realResult = specularColor;
		break;
		case D3DTA_TEMP:
			realResult = temp;
		break;
		case D3DTA_TEXTURE:
			realResult = texture(tex, texcoord.xy);
		break;
		case D3DTA_TFACTOR:
			realResult = vec4(0);
		break;
		default:
			realResult = vec4(0);
		break;
	}
		
	if((argument & D3DTA_ALPHAREPLICATE) != 0)
	{
		realResult.rgb = vec3(realResult.a,realResult.a,realResult.a);
	}

	if((argument & D3DTA_COMPLEMENT) != 0)
	{
		realResult = (1.0 - realResult);
	}
	
	return realResult;
}

vec4 getStageArgument(uint argument,vec4 temp,uint constant,vec4 result)
{
	vec4 realResult;
	switch(argument & D3DTA_SELECTMASK)
	{
		case D3DTA_CONSTANT:
			realResult = Convert(constant);
		break;
		case D3DTA_CURRENT:
			realResult = result;
		break;
		case D3DTA_DIFFUSE:
			realResult = diffuseColor;
		break;
		case D3DTA_SPECULAR:
			realResult = specularColor;
		break;
		case D3DTA_TEMP:
			realResult = temp;
		break;
		case D3DTA_TEXTURE:
			realResult = vec4(1,1,1,1);
		break;
		case D3DTA_TFACTOR:
			realResult = vec4(1,1,1,1);
		break;
		default:
			realResult = vec4(1,1,1,1);
		break;
	}
		
	if((argument & D3DTA_ALPHAREPLICATE) != 0)
	{
		realResult.rgb = vec3(realResult.a,realResult.a,realResult.a);
	}

	if((argument & D3DTA_COMPLEMENT) != 0)
	{
		realResult = (1.0 - realResult);
	}
	
	return realResult;
}

void processStage(sampler2D tex,uint textureIndex, uint constant, uint resultArgument,
vec4 resultIn, vec4 tempIn, out vec4 resultOut, out vec4 tempOut,
uint colorOperation, uint colorArgument1, uint colorArgument2, uint colorArgument0,
uint alphaOperation, uint alphaArgument1, uint alphaArgument2, uint alphaArgument0)
{
	vec4 temp = tempIn;
	vec4 result = resultIn;
	vec4 tempResult = resultIn; //This is the result regardless if selected target.
	vec2 texcoord = getTextureCoord(textureIndex);

	vec4 colorArg1 = getStageArgument(colorArgument1,tempIn,constant,resultIn, tex, texcoord);
	vec4 colorArg2 = getStageArgument(colorArgument2,tempIn,constant,resultIn, tex, texcoord);
	vec4 colorArg0 = getStageArgument(colorArgument0,tempIn,constant,resultIn, tex, texcoord);

	vec4 alphaArg1 = getStageArgument(alphaArgument1,tempIn,constant,resultIn, tex, texcoord);
	vec4 alphaArg2 = getStageArgument(alphaArgument2,tempIn,constant,resultIn, tex, texcoord);
	vec4 alphaArg0 = getStageArgument(alphaArgument0,tempIn,constant,resultIn, tex, texcoord);
	
	float alpha = calculateResult(alphaOperation,alphaArg1,alphaArg2,alphaArg0,1.0,1.0).a;

	if(renderState.alphaTestEnable==1)
	{
		float ref = renderState.alphaReference / 255.0f;
								
		switch(renderState.alphaFunction)
		{
		    case D3DCMP_NEVER:
				discard;
			break;
			case D3DCMP_LESS:
			if(alpha < ref){}else{discard;}
			break;
			case D3DCMP_EQUAL:
			if(alpha == ref){}else{discard;}
			break;
			case D3DCMP_LESSEQUAL:
			if(alpha <= ref){}else{discard;}
			break;
			case D3DCMP_GREATER:
			if(alpha > ref){}else{discard;}
			break;
			case D3DCMP_NOTEQUAL:
			if(alpha != ref){}else{discard;}
			break;
			case D3DCMP_GREATEREQUAL:
			if(alpha>= ref){}else{discard;}
			break;
			case D3DCMP_ALWAYS:		
			break;
		}
	}	
	
	if(colorOperation != D3DTOP_DISABLE)
	{
		tempResult.rgb = calculateResult(colorOperation,colorArg1,colorArg2,colorArg0,tempResult.a,tempResult.a).rgb;	
	}

	if(renderState.alphaBlendEnable==0)
	{
		tempResult.a = 1.0f;
	}
	else
	{
		if(alphaOperation != D3DTOP_DISABLE)
		{
			tempResult.a = alpha;
		}
	}

	switch(resultArgument)
	{
		case D3DTA_CURRENT:
			result = tempResult;
		break;
		case D3DTA_TEMP:
			temp = tempResult;
		break;
		default:
			result = tempResult;
		break;
	}

	resultOut = result;
	tempOut = temp;
}

void processStage(uint constant, uint resultArgument,
vec4 resultIn, vec4 tempIn, out vec4 resultOut, out vec4 tempOut,
uint colorOperation, uint colorArgument1, uint colorArgument2, uint colorArgument0,
uint alphaOperation, uint alphaArgument1, uint alphaArgument2, uint alphaArgument0)
{
	vec4 temp = tempIn;
	vec4 result = resultIn;
	vec4 tempResult = resultIn; //This is the result regardless if selected target.

	vec4 colorArg1 = getStageArgument(colorArgument1,tempIn,constant,resultIn);
	vec4 colorArg2 = getStageArgument(colorArgument2,tempIn,constant,resultIn);
	vec4 colorArg0 = getStageArgument(colorArgument0,tempIn,constant,resultIn);

	vec4 alphaArg1 = getStageArgument(alphaArgument1,tempIn,constant,resultIn);
	vec4 alphaArg2 = getStageArgument(alphaArgument2,tempIn,constant,resultIn);
	vec4 alphaArg0 = getStageArgument(alphaArgument0,tempIn,constant,resultIn);

	float alpha = calculateResult(alphaOperation,alphaArg1,alphaArg2,alphaArg0,1.0,1.0).a;

	if(renderState.alphaTestEnable==1)
	{
		float ref = renderState.alphaReference / 255.0f;
								
		switch(renderState.alphaFunction)
		{
		    case D3DCMP_NEVER:
				discard;
			break;
			case D3DCMP_LESS:
			if(alpha < ref){}else{discard;}
			break;
			case D3DCMP_EQUAL:
			if(alpha == ref){}else{discard;}
			break;
			case D3DCMP_LESSEQUAL:
			if(alpha <= ref){}else{discard;}
			break;
			case D3DCMP_GREATER:
			if(alpha > ref){}else{discard;}
			break;
			case D3DCMP_NOTEQUAL:
			if(alpha != ref){}else{discard;}
			break;
			case D3DCMP_GREATEREQUAL:
			if(alpha>= ref){}else{discard;}
			break;
			case D3DCMP_ALWAYS:		
			break;
		}
	}	
	
	if(colorOperation != D3DTOP_DISABLE)
	{
		tempResult.rgb = calculateResult(colorOperation,colorArg1,colorArg2,colorArg0,tempResult.a,tempResult.a).rgb;	
	}

	if(renderState.alphaBlendEnable==0)
	{
		tempResult.a = 1.0f;
	}
	else
	{
		if(alphaOperation != D3DTOP_DISABLE)
		{
			tempResult.a = alpha;
		}
	}

	switch(resultArgument)
	{
		case D3DTA_CURRENT:
			result = tempResult;
		break;
		case D3DTA_TEMP:
			temp = tempResult;
		break;
		default:
			result = tempResult;
		break;
	}

	resultOut = result;
	tempOut = temp;
}