/*
Copyright(c) 2017 Christopher Joseph Dean Schaefer

This software is provided 'as-is', without any express or implied
warranty.In no event will the authors be held liable for any damages
arising from the use of this software.

Permission is granted to anyone to use this software for any purpose,
including commercial applications, and to alter it and redistribute it
freely, subject to the following restrictions :

1. The origin of this software must not be misrepresented; you must not
claim that you wrote the original software.If you use this software
in a product, an acknowledgment in the product documentation would be
appreciated but is not required.
2. Altered source versions must be plainly marked as such, and must not be
misrepresented as being the original software.
3. This notice may not be removed or altered from any source distribution.
*/

layout(std140,binding = 2) uniform ShaderStateBlock2
{
	Light lights[8];
};

layout(std140,binding = 3) uniform ShaderStateBlock3
{
	Material material;
};

layout(std140,binding = 4) uniform ShaderStateBlock4
{
    mat4 worldViewProjection;
	mat4 world;
	mat4 view;
	mat4 projection;
	mat4 worldViewInverted;
	mat4 viewInverted;
} ubo;

struct ColorPair
{
	vec4   Diffuse;
	vec4   Specular;
};

ColorPair CalculateDirectionalLight(vec3 N, vec3 V, int i)
{
   ColorPair returnValue;
   vec3 L = mat3(ubo.viewInverted) * vec3(-normalize(lights[i].Direction));
   float NdotL = dot(N, L);
   returnValue.Diffuse = lights[i].Ambient;
   returnValue.Specular = vec4(0);
   if(NdotL > 0.0f)
   {
      //compute diffuse color
      returnValue.Diffuse += NdotL * lights[i].Diffuse;

      //add specular component
      if(renderState.specularEnable==1)
      {
         vec3 H = normalize(L + V);   //half vector
         returnValue.Specular = pow(max(0, dot(H, N)), material.Power) * lights[i].Specular;
      }
   }
   return returnValue;
}

ColorPair CalculatePointLight(vec4 vPosition, vec3 N, vec3 V, int i)
{
   vec3 L = mat3(ubo.viewInverted) * normalize(vec3(lights[i].Position-(ubo.world * vPosition)));
   ColorPair returnValue;
   float NdotL = dot(N, L);
   returnValue.Diffuse = lights[i].Ambient;
   returnValue.Specular = vec4(0);
   float fAtten = 1.f;
   if(NdotL >= 0.f)
   {
      //compute diffuse color
      returnValue.Diffuse += NdotL * lights[i].Diffuse;

      //add specular component
      if(renderState.specularEnable==1)
      {
         vec3 H = normalize(L + V);   //half vector
         returnValue.Specular = pow(max(0, dot(H, N)), material.Power) * lights[i].Specular;
      }

      float LD = length(vec3(lights[i].Position)-vec3(ubo.world * vPosition));
      if(LD > lights[i].Range)
      {
         fAtten = 0.f;
      }
      else
      {
         fAtten *= 1.f/(lights[i].Attenuation0 + lights[i].Attenuation1*LD + lights[i].Attenuation2*LD*LD);
      }
      returnValue.Diffuse *= fAtten;
      returnValue.Specular *= fAtten;
   }
   return returnValue;
}

ColorPair CalculateSpotLight(vec4 vPosition, vec3 N, vec3 V, int i)
{
   vec3 L = mat3(ubo.viewInverted) * normalize(vec3(lights[i].Position-(ubo.world * vPosition)));
   ColorPair returnValue;
   float NdotL = dot(N, L);
   returnValue.Diffuse = lights[i].Ambient;
   returnValue.Specular = vec4(0);
   float fAttenSpot = 1.f;
   if(NdotL >= 0.f)
   {
      //compute diffuse color
      returnValue.Diffuse += NdotL * lights[i].Diffuse;

      //add specular component
      if(renderState.specularEnable==1)
      {
         vec3 H = normalize(L + V);   //half vector
         returnValue.Specular = pow(max(0, dot(H, N)), material.Power) * lights[i].Specular;
      }

      float LD = length(vec3(lights[i].Position)-vec3(ubo.world * vPosition));
      if(LD > lights[i].Range)
      {
         fAttenSpot = 0.f;
      }
      else
      {
         fAttenSpot *= 1.f/(lights[i].Attenuation0 + lights[i].Attenuation1*LD + lights[i].Attenuation2*LD*LD);
      }

      //spot cone computation
      vec3 L2 = mat3(ubo.viewInverted) * vec3(-normalize(lights[i].Direction));
      float rho = dot(L, L2);
	  vec3 vSpot = vec3(cos(lights[i].Theta/2),cos(lights[i].Phi/2),cos(lights[i].Falloff));
      fAttenSpot *= pow(clamp((rho - vSpot.y)/(vSpot.x - vSpot.y),0.0,1.0), vSpot.z);

      returnValue.Diffuse *= fAttenSpot;
      returnValue.Specular *= fAttenSpot;
   }
   return returnValue;
}

ColorPair CalculateGlobalIllumination(vec4 vPosition, vec3 N, vec3 V, int i)
{
	ColorPair returnValue;

	return returnValue;
}

/*
https://msdn.microsoft.com/en-us/library/windows/desktop/bb172256(v=vs.85).aspx
*/
vec4 GetGlobalIllumination(vec4 norm)
{
	vec4 ambient = vec4(0);
	vec4 diffuse = vec4(0);
	vec4 specular = vec4(0);
	vec4 emissive = vec4(0);	
	vec4 ambientTemp = vec4(0);
	vec4 diffuseTemp = vec4(0);
	vec4 specularTemp = vec4(0);
	vec3 cameraPosition = vec3(0);

	float lightDistance = 0;
	vec4 vectorPosition = vec4(0);
	vec4 lightPosition = vec4(0);
	vec4 lightDirection = vec4(0);
	float attenuation = 0;
	vec4 ldir = vec4(0);
	float rho = 0;
	float spot = 0;

	vectorPosition = vec4(mat3(ubo.world) * position.xyz,1.0);
	vectorPosition *= vec4(1.0,-1.0,1.0,1.0);

	//https://msdn.microsoft.com/en-us/library/windows/desktop/bb172279(v=vs.85).aspx
	for( int i=0; i<8; ++i )
	{		
		if(lights[i].IsEnabled==1)
		{		
			lightPosition = ubo.world * lights[i].Position;
			lightPosition *= vec4(1.0,-1.0,1.0,1.0);

			lightDirection = lights[i].Direction;
			lightDirection *= vec4(1.0,-1.0,1.0,1.0);

			lightDistance = abs(distance(vectorPosition.xyz,lightPosition.xyz));

			if(lights[i].Type == D3DLIGHT_DIRECTIONAL)
			{
				ldir = normalize(lightDirection * vec4(-1.0,-1.0,-1.0,-1.0));
			}
			else
			{
				ldir = normalize(lightPosition - vectorPosition);
			}

			if(lights[i].Type == D3DLIGHT_DIRECTIONAL)
			{
				attenuation = 1;
			}
			else if(lights[i].Range < lightDistance)
			{
				attenuation = 0;
			}
			else
			{
				attenuation = 1/( lights[i].Attenuation0 + lights[i].Attenuation1 * lightDistance + lights[i].Attenuation2 * pow(lightDistance,2));	
			}

			rho = clamp(dot(normalize(lightDirection.xyz * vec3(-1.0,-1.0,-1.0)),normalize(lightPosition.xyz - vectorPosition.xyz)),0,1);

			if(lights[i].Type != D3DLIGHT_SPOT || rho > cos(lights[i].Theta/2))
			{
				spot = 1;
			}
			else if(rho <= cos(lights[i].Phi/2))
			{
				spot = 0;
			}
			else
			{
				float u = rho - cos(lights[i].Phi / 2);
				float v = cos(lights[i].Theta / 2) - cos(lights[i].Phi / 2);
				spot = pow(clamp(u / v,0,1),lights[i].Falloff);
			}

			ambientTemp += (attenuation * spot * lights[i].Ambient);
			diffuseTemp += (diffuseColor * lights[i].Diffuse * clamp(dot(norm,ldir),0,1) * attenuation * spot);

			if(renderState.specularEnable==1)
			{
				vec3 halfway;
				if(renderState.localViewer==1)
				{
					halfway = normalize(normalize(cameraPosition - position.xyz) + ldir.xyz);			
				}
				else
				{
					halfway = normalize(vec3(0,0,1) + ldir.xyz);
				}
				
				//The result is defined if x < 0 or x=0 and y<=0
				// The clamp will take care of the former and the if will take care of the latter. if the result is zero then this whole line is zero therefore just skip it.
				if(material.Power > 0)
				{
					specularTemp += (lights[i].Specular * pow(clamp(dot(norm.xyz,halfway),0,1),material.Power) * attenuation * spot);
				}
			}
		}
	}

	ambient = material.Ambient * (Convert(renderState.ambient) + ambientTemp);
	diffuse = diffuseTemp;
	emissive = emissiveColor;

	if(renderState.specularEnable==1)
	{
		specular = specularColor * specularTemp;
	}

	vec4 lightColor = (ambient + diffuse + specular + emissive);
	lightColor.a = 1.0;
	return lightColor;
}